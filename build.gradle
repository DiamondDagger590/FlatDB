plugins {
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'com.cyr1en'
def versionObj = new Version(major: 1, minor: 0, state: State.NO_STATE)
version "${versionObj.toString()}"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jetbrains', name: 'annotations', version: '16.0.2'
    implementation group: 'com.h2database', name: 'h2', version: '1.4.198'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'

    testImplementation(platform('org.junit:junit-bom:5.7.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.12.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.198'
}

shadowJar {
    archiveName = "${project.name}-${version}.jar"
}

artifacts {
    println("Tag: ${versionObj.getPatch()}")
    archives shadowJar
}

lombok {
    version = "1.18.4"
    sha256 = ""
}

class Version {

    String major, minor, full
    State state

    def getFullVersion() {
        full = "$major.$minor.${getPatch()}${state.get()}"
        return full
    }

    static def getPatch() {
        def proc = "git describe --tags".execute()
        proc.waitFor()
        def tagInfo = proc.text
        if (!tagInfo.contains('-'))
            return 0
        def currentTag = tagInfo.split('-')[1]
        return currentTag
    }

    String toString() {
        getFullVersion()
    }
}

enum State {
    ALPHA, BETA, STABLE, NO_STATE

    String get() {
        if(this.name().equalsIgnoreCase('no_state'))
            return ""
        return this.name().equalsIgnoreCase("stable") ? "" : "-" + this.name()
    }
}
